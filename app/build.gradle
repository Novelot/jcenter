apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.0'
}

//apply from: 'bintray.gradle'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
group = PROJ_GROUP
version = PROJ_VERSION
//
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = hasProperty("bintrayUser") ? getProperty("bintrayUser") : getProperty("BINTRAY_USER")
    key = hasProperty("bintrayKey") ? getProperty("bintrayKey") : getProperty("BINTRAY_KEY")

    configurations = ['archives']
    publications = ['MyPublication']
    publish = true

    pkg {
        repo = 'maven'
        name = PROJ_NAME
        desc = PROJ_DESCRIPTION
        websiteUrl = PROJ_WEBSITEURL
        issueTrackerUrl = PROJ_ISSUETRACKERURL
        vcsUrl = PROJ_VCSURL
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true

        version {
            name = PROJ_VERSION
            vcsTag = PROJ_VERSION
        }
    }
}
//
//publishing {
//    publications {
//        MyPublication(MavenPublication) {
//            groupId PROJ_GROUP
//            artifactId PROJ_NAME
//            version PROJ_VERSION
//        }
//    }
//}

publishing {
    publications {
            MyPublication(MavenPublication) {
            groupId group
            version version
            artifactId PROJ_NAME
            artifact "build/outputs/aar/app-release.aar"
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', PROJ_ARTIFACTID)
                root.appendNode('description', PROJ_DESCRIPTION)
                root.appendNode('url', PROJ_WEBSITEURL)

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', PROJ_ISSUETRACKERURL)

                def scm = root.appendNode('scm')
                scm.appendNode('url', PROJ_VCSURL)
                scm.appendNode('connection', PROJ_VCSURL)
                scm.appendNode('developerConnection', PROJ_SCM_DEV_CONNECTION)

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', PROJ_LICENCE_NAME)
                license.appendNode('url', PROJ_LICENCE_URL)
                license.appendNode('distribution', PROJ_LICENCE_DIST)
            }
        }
    }
}